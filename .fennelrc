(local lua-version "5.4")
(local path-patterns {:path ["/?.lua" "/?/init.lua"]
                      :cpath ["/?.so"]})
(local path-luarocks-dirs {:path ".luarocks/share/lua"
                           :cpath ".luarocks/lib/lua"})

(fn luarocks-paths
  [path-name]
  (let [paths []
        path (. path-luarocks-dirs path-name)]
    (each [_ prefix (ipairs ["" (.. (os.getenv "HOME") "/")])]
      (table.insert paths (.. prefix  path "/" lua-version)))
    paths))

(fn add-to-path
  [path-name dirs]
  (let [paths []
        luarocks-dirs (luarocks-paths path-name)
        dirs (or dirs [])]
    (each [_ dir-tbl (ipairs [dirs luarocks-dirs])]
      (each [_ dir (ipairs dir-tbl)]
        (each [_ patt (ipairs (. path-patterns path-name))]
          (table.insert paths (.. dir patt)))))
    (let [paths (table.concat paths ";")
          path-var (. package path-name)]
      (tset package path-name (.. paths ";" path-var)))))

(add-to-path :path ["src"])
(add-to-path :cpath)

; vim:ft=fennel
